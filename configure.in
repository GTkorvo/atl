dnl Process this file with autoconf to produce a configure script.
AC_INIT(atom.c)
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(atl,0.1)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

AC_CANONICAL_HOST
kernel_build=false
AC_ARG_ENABLE(kernel-build,[Build for inclusion in the kernel],
[
case "${host_os}" in
linux-gnu*)
	AC_MSG_WARN("building linux kernel")
	CPPFLAGS="$CPPFLAGS -DMODULE -DLINUX"
	if test $ac_cv_prog_gcc = yes; then
		CFLAGS="-fomit-frame-pointer -g -O2"
	fi
	KERNEL_OBJS="katl.lo env.lo strtol.lo"
	KERNEL_VERSION=`awk -F\" '/REL/ {print $$2}' /usr/include/linux/version.h`
	KERNEL_TARGET_MOD="kernel_"
	kernel_build=true
	enable_shared=no
	;;
*)
	AC_MSG_WARN("--enable-kernel-build ignored on non Linux platforms")
	;;
esac
])
AM_CONDITIONAL(KERNEL, test x$kernel_build = xtrue)

dnl Configure libtool
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
test "$CC" = "gcc" && CFLAGS="$CFLAGS -Wall"

CPPFLAGS="$CPPFLAGS -DATL_SOURCE"
LIB_PRE=lib
LIB_POST=.a
case "$host_os" in
  winnt* | cygwin )
    LIB_POST=".lib"
    LIB_PRE=""
    CFLAGS="$CFLAGS /MT -I."
    ;;
esac

AC_SUBST(KERNEL_OBJS)
AC_SUBST(KERNEL_VERSION)
AC_SUBST(KERNEL_TARGET_MOD)

dnl Checks for libraries.
AC_REQUIRE_HPPCEL_PACKAGE(pbio, io.h, ${LIB_PRE}IO${LIB_POST})
AC_REQUIRE_HPPCEL_PACKAGE(gen_thread, gen_thread.h,libgen_thread.la)

if test -n "$ac_cv_gen_thread_link_arg"; then
LIBS="$LIBS -lgen_thread"
fi

checkBoth=0
AC_CHECK_FUNC(connect, checkSocket=0, checkSocket=1)
if test "$checkSocket" = 1; then
    AC_CHECK_LIB(socket, connect, LIBS="$LIBS -lsocket", checkBoth=1)
    if test "$checkBoth" = 1; then
        oldLibs=$LIBS
        LIBS="$LIBS -lsocket -lnsl"
        AC_CHECK_FUNC(accept, checkNsl=0, [LIBS=$oldLibs])
    fi
   AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, getdomainname, [LIBS="$LIBS -lnsl"]))
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(malloc.h sys/time.h unistd.h windows.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_GETPEERNAME_DEFINED
AC_PUTENV_DEFINED
AC_FSCANF_DEFINED
AC_SPRINTF_DEFINED
AC_SSCANF_DEFINED

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(gethostname putenv strdup)

AC_OUTPUT(Makefile)
