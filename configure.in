dnl Process this file with autoconf to produce a configure script.
AC_INIT([atl],[2.0],[eisen@cc.gatech.edu])
AC_CONFIG_SRCDIR([atom.c])
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(AC_PACKAGE_TARNAME,AC_PACKAGE_VERSION)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_EXEEXT
AC_PROG_INSTALL

AC_CANONICAL_HOST
kernel_build=false
AC_ARG_ENABLE(kernel-build,[Build for inclusion in the kernel],
[
case "${host_os}" in
linux-gnu*)
	AC_MSG_WARN("building linux kernel")
	CPPFLAGS="$CPPFLAGS -DMODULE -DLINUX"
	if test $ac_cv_prog_gcc = yes; then
		CFLAGS="-fomit-frame-pointer -g -O2"
	fi
	KERNEL_OBJS="katl.lo env.lo strtol.lo"
	KERNEL_VERSION=`awk -F\" '/REL/ {print $$2}' /usr/include/linux/version.h`
	KERNEL_TARGET_MOD="kernel_"
	kernel_build=true
	enable_shared=no
	;;
*)
	AC_MSG_WARN("--enable-kernel-build ignored on non Linux platforms")
	;;
esac
])
AM_CONDITIONAL(KERNEL, test x$kernel_build = xtrue)

dnl Configure libtool
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
LIBTOOL="$LIBTOOL --silent"
AC_SUBST(LIBTOOL_DEPS)
test "$CC" = "gcc" && CFLAGS="$CFLAGS -Wall"

CPPFLAGS="$CPPFLAGS -DATL_SOURCE"
LIB_PRE=lib
LIB_POST=.a
case "$host_os" in
  winnt* | cygwin | mingw* )
    LIB_POST=".lib"
    LIB_PRE=""
    SOCKET_LIBS="$LIBS -lwsock32 -luser32 -ladvapi32"
    ;;
esac

AC_SUBST(KERNEL_OBJS)
AC_SUBST(KERNEL_VERSION)
AC_SUBST(KERNEL_TARGET_MOD)

dnl Checks for libraries.
AC_REQUIRE_HPPCEL_PACKAGE(pbio, io.h, ${LIB_PRE}IO${LIB_POST})
AC_REQUIRE_HPPCEL_PACKAGE(gen_thread, gen_thread.h,libgen_thread.la)
if test -n "$ac_cv_gen_thread_link_arg"; then
THREAD_LIBS="-lgen_thread"
fi

checkBoth=0
AC_CHECK_FUNC(connect, checkSocket=0, checkSocket=1)
save_libs=$LIBS;
if test "$checkSocket" = 1; then
    AC_CHECK_LIB(socket, connect, LIBS="$LIBS -lsocket", checkBoth=1)
    if test "$checkBoth" = 1; then
        oldLibs=$LIBS
        LIBS="$LIBS -lsocket -lnsl"
        AC_CHECK_FUNC(accept, checkNsl=0, [LIBS=$oldLibs])
    fi
   AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, getdomainname, [LIBS="$LIBS -lnsl"]))
fi
SOCKET_LIBS="$SOCKET_LIBS $LIBS"
LIBS=$save_libs
AC_SUBST(SOCKET_LIBS)
dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(malloc.h sys/time.h unistd.h windows.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_GETPEERNAME_DEFINED
AC_PUTENV_DEFINED
AC_FSCANF_DEFINED
AC_SPRINTF_DEFINED
AC_SSCANF_DEFINED

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(fork gethostname putenv strdup)

AC_ARG_WITH(atom_server, [  --with-atom-server=HOST    Which host will run the atom server.],ATOM_SERVER_HOST=$withval)

if test "x$ATOM_SERVER_HOST" = "x"; then
   hostname=`hostname`
   domainname=""
   host_no_domain=`hostname|sed 's/\..*//g'`
   if test "$hostname" != "$host_no_domain"; then
      # A fully qualified hostname?
      domainname=`hostname|sed "s/$host_no_domain.//"`
      hostname=$host_no_domain
   fi
   if test x"$domainname" = "x"; then
	if eval dnsdomainname 2>/dev/null 1>/dev/null; then
	    # dnsdomainname command, try it
	    domainname=`dnsdomainname`
	fi
   fi
   if test x"$domainname" = "x"; then
	if eval domainname 2>/dev/null 1>/dev/null; then
	    domainname=`domainname`
	fi
   fi
   if test x"$hostname" = "x"; then
	hostname="localhost"
   fi
   if test x"$domainname" = "x"; then
      # domainname is still not set, try this
      domainname=[`nslookup $hostname 2>/dev/null | grep Name: | sed 's/.*: *[^.]*.//g'`]
   fi

   highdomain=`echo $domainname | sed 's/\./\\
./g' | tail -2 `
    highdomain=`echo $highdomain | sed 's/ //g'`
   if test "x$highdomain" = "x.gatech.edu"; then
      ATOM_SERVER_HOST="marquesas.cc.gatech.edu";
      ATOM_SERVICE_DOMAIN="gt.atl.ga.us:gatech.edu";
   else
      if test "x$domainname" != "x"; then
          ATOM_SERVER_HOST=$hostname.$domainname
      else
          ATOM_SERVER_HOST=$hostname
      fi
   fi
fi
AC_ARG_WITH(service_domain, [  --with-service-domain=DOMAINNAME	What domain the server will serve.],ATOM_SERVER_DOMAIN=$withval)
AC_DEFINE_UNQUOTED(ATOM_SERVER_HOST, "$ATOM_SERVER_HOST", [Define with the name of the host which will run the atom server])
AC_DEFINE_UNQUOTED(ATOM_SERVICE_DOMAIN, "$ATOM_SERVICE_DOMAIN", [Define with the name of the domain which the atom server will service])
SERVER_LIBS=$LIBS
AC_SUBST(SERVER_LIBS)
AC_SUBST(THREAD_LIBS)

AC_CONFIG_FILES(Makefile)
AC_OUTPUT
